
pragma solidity 0.6.11;

import "./Interfaces/IWAsset.sol";
import "./Dependencies/TroveManagerBase.sol";

/**
 * TroveManagerRedemptions is derived from TroveManager and handles all redemption activity of troves.
 * Instead of calculating redemption fees in ETH like Liquity used to, we now calculate it as a portion
 * of YUSD passed in to redeem. The YUSDAmount is still how much we would like to redeem, but the
 * YUSDFee is now the maximum amount of YUSD extra that will be paid and must be in the balance of the
 * redeemer for the redemption to succeed. This fee is the same as before in terms of percentage of value,
 * but now it is in terms of YUSD. We now use a helper function to be able to estimate how much YUSD will
 * be actually needed to perform a redemption of a certain amount, and also given an amount of YUSD balance,
 * the max amount of YUSD that can be used for a redemption, and a max fee such that it will always go through.
 *
 * Given a balance of YUSD, Z, the amount that can actually be redeemed is :
 * Y = YUSD you can actually redeem
 * BR = decayed base rate
 * X = YUSD Fee
 * S = Total YUSD Supply
 * The redemption fee rate is = (Y / S * 1 / BETA + BR + 0.5%)
 * This is because the new base rate = BR + Y / S * 1 / BETA
 * We pass in X + Y = Z, and want to find X and Y.
 * Y is calculated to be = S * (sqrt((1.005 + BR)**2 + BETA * Z / S) - 1.005 - BR)
 * If you want to calculate the real values programatically you can use the following formula that accounts for all the various decimal places:
 * S * (sqrt(int(1e18)*(((int(1005e15) + BR)**2)+(BETA * Z*int(1e36) / S))) - (int(1005e15) + BR)*int(1e9))/int(1e27)
 * through the quadratic formula, and X = Z - Y.
 * Therefore the amount we can actually redeem given Z is Y, and the max fee is X.
 *
 * To find how much the fee is given Y, we can multiply Y by the new base rate, which is BR + Y / S * 1 / BETA.
 * If you want to calculate the real values programatically you can use the following formula that accounts for all the various decimal places:
 * (((Y*int(1e18) / S) / BETA) + BR + int(5e15))*Y/int(1e18)
 *
 * To the redemption function, we pass in Y and X.
 */

contract TroveManagerRedemptions is TroveManagerBase {
    struct RedemptionTotals {
        uint256 remainingYUSD;
        uint256 totalYUSDToRedeem;
        newColls CollsDrawn;
        uint256 YUSDfee;
        uint256 decayedBaseRate;
        uint256 totalYUSDSupplyAtStart;
        uint256 maxYUSDFeeAmount;
    }

    /*
     * BETA: 18 digit decimal. Parameter by which to divide the redeemed fraction, in order to calc the new base rate from a redemption.
     * Corresponds to (1 / ALPHA) in the white paper.
     */
    uint256 public constant BETA = 2;

    uint256 public constant BOOTSTRAP_PERIOD = 14 days;

    event Redemption(
        uint256 _attemptedYUSDAmount,
        uint256 _actualYUSDAmount,
        uint256 YUSDfee,
        address[] tokens,
        uint256[] amounts
    );

    function setAddresses(
        address _borrowerOperationsAddress,
        address _activePoolAddress,
        address _defaultPoolAddress,
        address _stabilityPoolAddress,
        address _gasPoolAddress,
        address _collSurplusPoolAddress,
        address _yusdTokenAddress,
        address _sortedTrovesAddress,
        address _yetiTokenAddress,
        address _sYETIAddress,
        address _whitelistAddress,
        address _troveManagerAddress
    ) external onlyOwner {
        checkContract(_borrowerOperationsAddress);
        checkContract(_activePoolAddress);
        checkContract(_defaultPoolAddress);
        checkContract(_stabilityPoolAddress);
        checkContract(_gasPoolAddress);
        checkContract(_collSurplusPoolAddress);
        checkContract(_yusdTokenAddress);
        checkContract(_sortedTrovesAddress);
        checkContract(_yetiTokenAddress);
        checkContract(_sYETIAddress);
        checkContract(_whitelistAddress);
        checkContract(_troveManagerAddress);

        borrowerOperationsAddress = _borrowerOperationsAddress;
        activePool = IActivePool(_activePoolAddress);
        defaultPool = IDefaultPool(_defaultPoolAddress);
        stabilityPoolContract = IStabilityPool(_stabilityPoolAddress);
        whitelist = IWhitelist(_whitelistAddress);
        gasPoolAddress = _gasPoolAddress;
        collSurplusPool = ICollSurplusPool(_collSurplusPoolAddress);
        yusdTokenContract = IYUSDToken(_yusdTokenAddress);
        sortedTroves = ISortedTroves(_sortedTrovesAddress);
        yetiTokenContract = IYETIToken(_yetiTokenAddress);
        sYETIContract = ISYETI(_sYETIAddress);
        troveManager = ITroveManager(_troveManagerAddress);
        troveManagerAddress = _troveManagerAddress;

        emit BorrowerOperationsAddressChanged(_borrowerOperationsAddress);
        emit ActivePoolAddressChanged(_activePoolAddress);
        emit DefaultPoolAddressChanged(_defaultPoolAddress);
        emit StabilityPoolAddressChanged(_stabilityPoolAddress);
        emit GasPoolAddressChanged(_gasPoolAddress);
        emit CollSurplusPoolAddressChanged(_collSurplusPoolAddress);
        emit YUSDTokenAddressChanged(_yusdTokenAddress);
        emit SortedTrovesAddressChanged(_sortedTrovesAddress);
        emit YETITokenAddressChanged(_yetiTokenAddress);
        emit SYETIAddressChanged(_sYETIAddress);

        _renounceOwnership();
    }

    /**
     * @notice
     * Main function for redeeming collateral. See above for how YUSDMaxFee is calculated.
     * _YUSDamount + _YUSDMaxFee must be less than the balance of the sender.
     */
    /// @param _YUSDamount is equal to the amount of YUSD to actually redeem.
    /// @param _YUSDMaxFee is equal to the max fee in YUSD that the sender is willing to pay
    /// @param _firstRedemptionHint The address of the trove being redeemed against
    /// @param _upperPartialRedemptionHint is the address of the adjacent Trove with a greater ICR than the trove after being partially redeemed against
    /// @param _lowerPartialRedemptionHint is the address of the adjacent Trove with a lower ICR than the trove after being partially redeemed against
    /// @param _partialRedemptionHintICR is the new ICR of the trove after being partially redeemed against
    /// @param _maxIterations is the maximum number of iterations to perform before giving up. Only relevant if the hints are a little off by the type of redemption.
    /// @param _redeemer is the address of the agent initiating the redemption. It will be msg.sender when called through troveManager.
    function redeemCollateral(
        uint256 _YUSDamount,
        uint256 _YUSDMaxFee,
        address _firstRedemptionHint,
        address _upperPartialRedemptionHint,
        address _lowerPartialRedemptionHint,
        uint256 _partialRedemptionHintICR,
        uint256 _maxIterations,
        address _redeemer
    ) external {
        _requireCallerisTroveManager();
        ContractsCache memory contractsCache = ContractsCache(
            activePool,
            defaultPool,
            yusdTokenContract,
            sYETIContract,
            sortedTroves,
            collSurplusPool,
            gasPoolAddress
        );
        RedemptionTotals memory totals;

        _requireYUSDBalanceCoversRedemption(contractsCache.yusdToken, _redeemer, _YUSDamount);
        _requireValidMaxFee(_YUSDamount, _YUSDMaxFee);
        _requireAfterBootstrapPeriod();
        _requireTCRoverMCR();
        _requireAmountGreaterThanZero(_YUSDamount);

        totals.totalYUSDSupplyAtStart = getEntireSystemDebt();

        // Confirm redeemer's balance is less than total YUSD supply
        assert(contractsCache.yusdToken.balanceOf(_redeemer) <= totals.totalYUSDSupplyAtStart);

        totals.remainingYUSD = _YUSDamount;
        address currentBorrower;
        if (_isValidFirstRedemptionHint(contractsCache.sortedTroves, _firstRedemptionHint)) {
            currentBorrower = _firstRedemptionHint;
        } else {
            currentBorrower = contractsCache.sortedTroves.getLast();
            // Find the first trove with ICR >= MCR
            while (
                currentBorrower != address(0) && troveManager.getCurrentICR(currentBorrower) < MCR
            ) {
                currentBorrower = contractsCache.sortedTroves.getPrev(currentBorrower);
            }
        }
        // Loop through the Troves starting from the one with lowest collateral ratio until _amount of YUSD is exchanged for collateral
        if (_maxIterations == 0) {
            _maxIterations = uint256(-1);
        }
        while (currentBorrower != address(0) && totals.remainingYUSD > 0 && _maxIterations > 0) {
            _maxIterations--;
            // Save the address of the Trove preceding the current one, before potentially modifying the list
            address nextUserToCheck = contractsCache.sortedTroves.getPrev(currentBorrower);

            if (troveManager.getCurrentICR(currentBorrower) >= MCR) {
                troveManager.applyPendingRewards(currentBorrower);

                SingleRedemptionValues memory singleRedemption = _redeemCollateralFromTrove(
                    contractsCache,
                    _redeemer,
                    currentBorrower,
                    totals.remainingYUSD,
                    _upperPartialRedemptionHint,
                    _lowerPartialRedemptionHint,
                    _partialRedemptionHintICR
                );

                if (singleRedemption.cancelledPartial) break; // Partial redemption was cancelled (out-of-date hint, or new net debt < minimum), therefore we could not redeem from the last Trove

                totals.totalYUSDToRedeem = totals.totalYUSDToRedeem.add(singleRedemption.YUSDLot);

                totals.CollsDrawn = _sumColls(totals.CollsDrawn, singleRedemption.CollLot);
                totals.remainingYUSD = totals.remainingYUSD.sub(singleRedemption.YUSDLot);
            }

            currentBorrower = nextUserToCheck;
        }

        _requireNonZeroRedemptionAmount(totals.CollsDrawn);
        // Decay the baseRate due to time passed, and then increase it according to the size of this redemption.
        // Use the saved total YUSD supply value, from before it was reduced by the redemption.
        _updateBaseRateFromRedemption(totals.totalYUSDToRedeem, totals.totalYUSDSupplyAtStart);

        totals.YUSDfee = _getRedemptionFee(totals.totalYUSDToRedeem);
        // check user has enough YUSD to pay fee and redemptions
        _requireYUSDBalanceCoversRedemption(
            contractsCache.yusdToken,
            _redeemer,
            _YUSDamount.add(totals.YUSDfee)
        );

        // check to see that the fee doesn't exceed the max fee
        _requireUserAcceptsFeeRedemption(totals.YUSDfee, _YUSDMaxFee);

        // send fee from user to YETI stakers
        contractsCache.yusdToken.transferFrom(
            _redeemer,
            address(contractsCache.sYETI),
            totals.YUSDfee
        );

        emit Redemption(
            _YUSDamount,
            totals.totalYUSDToRedeem,
            totals.YUSDfee,
            totals.CollsDrawn.tokens,
            totals.CollsDrawn.amounts
        );
        // Burn the total YUSD that is cancelled with debt
        contractsCache.yusdToken.burn(_redeemer, totals.totalYUSDToRedeem);
        // Update Active Pool YUSD, and send Collaterals to account
        contractsCache.activePool.decreaseYUSDDebt(totals.totalYUSDToRedeem);

        contractsCache.activePool.sendCollateralsUnwrap(
            _redeemer,
            totals.CollsDrawn.tokens,
            totals.CollsDrawn.amounts,
            false
        );
    }

    /**
     * @notice
     * Redeem as much collateral as possible from _borrower's Trove in exchange for YUSD up to _maxYUSDamount
     * Special calculation for determining how much collateral to send of each type to send.
     * We want to redeem equivalent to the USD value instead of the VC value here, so we take the YUSD amount
     * which we are redeeming from this trove, and calculate the ratios at which we would redeem a single
     * collateral type compared to all others.
     * For example if we are redeeming 10,000 from this trove, and it has collateral A with a safety ratio of 1,
     * collateral B with safety ratio of 0.5. Let's say their price is each 1. The trove is composed of 10,000 A and
     * 10,000 B, so we would redeem 5,000 A and 5,000 B, instead of 6,666 A and 3,333 B. To do calculate this we take
     * the USD value of that collateral type, and divide it by the total USD value of all collateral types. The price
     * actually cancels out here so we just do YUSD amount * token amount / total USD value, instead of
     * YUSD amount * token value / total USD value / token price, since we are trying to find token amount.
     * _contractsCache is used to limit scope/gas savings
     */
    /// @param _redeemCaller Address of the redeemer as referenced in redeemCollateral(). It will be msg.sender when called from TroveManager
    /// @param _borrower Address of the borrower whose Trove is being redeemed
    /// @param _maxYUSDAmount Maximum amount of YUSD to redeem
    /// @param _upperPartialRedemptionHint The upper partial redemption hint referenced in redeemCollateral()
    /// @param _lowerPartialRedemptionHint The lower partial redemption hint referenced in redeemCollateral()
    /// @param _partialRedemptionHintICR The partial redemption hint ICR referenced in redeemCollateral()
    /// @return singleRedemption The total YUSD redeemed, the total collateral redeemed, and if the redemption was cancelled due to a partial redemption hint being out of date or new net debt < minimum. Struct referenced in TroveManagerBase.sol
    function _redeemCollateralFromTrove(
        ContractsCache memory _contractsCache,
        address _redeemCaller,
        address _borrower,
        uint256 _maxYUSDAmount,
        address _upperPartialRedemptionHint,
        address _lowerPartialRedemptionHint,
        uint256 _partialRedemptionHintICR
    ) internal returns (SingleRedemptionValues memory singleRedemption) {
        // Determine the remaining amount (lot) to be redeemed, capped by the entire debt of the Trove minus the liquidation reserve
        singleRedemption.YUSDLot = LiquityMath._min(
            _maxYUSDAmount,
            troveManager.getTroveDebt(_borrower).sub(YUSD_GAS_COMPENSATION)
        );

        newColls memory colls;
        (colls.tokens, colls.amounts, ) = troveManager.getCurrentTroveState(_borrower);

        uint256[] memory finalAmounts = new uint256[](colls.tokens.length);

        uint256 totalCollUSD = _getUSDColls(colls);
        uint256 baseLot = singleRedemption.YUSDLot.mul(DECIMAL_PRECISION);

        // redemption addresses are the same as coll addresses for trove
        // Calculation for how much collateral to send of each type.
        singleRedemption.CollLot.tokens = colls.tokens;
        singleRedemption.CollLot.amounts = new uint256[](colls.tokens.length);
        for (uint256 i = 0; i < colls.tokens.length; i++) {
            uint tokenAmountToRedeem = baseLot.mul(colls.amounts[i]).div(totalCollUSD).div(10**(whitelist.getDecimals(colls.tokens[i])));
            finalAmounts[i] = colls.amounts[i].sub(tokenAmountToRedeem);
            singleRedemption.CollLot.amounts[i] = tokenAmountToRedeem;
            // if it is a wrapped asset we need to reduce reward.
            // Later the asset will be transferred directly out, so no new reward is needed to be kept track of
            if (whitelist.isWrapped(colls.tokens[i])) {
                IWAsset(colls.tokens[i]).updateReward(_borrower, _redeemCaller, tokenAmountToRedeem);
            }
        }

        // Decrease the debt and collateral of the current Trove according to the YUSD lot and corresponding Collateral to send
        uint256 newDebt = (troveManager.getTroveDebt(_borrower)).sub(singleRedemption.YUSDLot);
        uint256 newColl = _getVC(colls.tokens, finalAmounts); // VC given newAmounts in trove

        if (newDebt == YUSD_GAS_COMPENSATION) {
            // No debt left in the Trove (except for the liquidation reserve), therefore the trove gets closed
            troveManager.removeStakeTMR(_borrower);
            troveManager.closeTroveRedemption(_borrower);
            _redeemCloseTrove(
                _contractsCache,
                _borrower,
                YUSD_GAS_COMPENSATION,
                colls.tokens,
                finalAmounts
            );

            address[] memory emptyTokens = new address[](0);
            uint256[] memory emptyAmounts = new uint256[](0);

            emit TroveUpdated(
                _borrower,
                0,
                emptyTokens,
                emptyAmounts,
                TroveManagerOperation.redeemCollateral
            );
        } else {
            uint256 newICR = LiquityMath._computeCR(newColl, newDebt);

            /*
             * If the provided hint is too inaccurate of date, we bail since trying to reinsert without a good hint will almost
             * certainly result in running out of gas. Arbitrary measures of this mean newICR must be greater than hint ICR - 2%,
             * and smaller than hint ICR + 2%.
             *
             * If the resultant net debt of the partial is less than the minimum, net debt we bail.
             */

            if (newICR >= _partialRedemptionHintICR.add(2e16) ||
            newICR <= _partialRedemptionHintICR.sub(2e16) ||
                _getNetDebt(newDebt) < MIN_NET_DEBT) {
                singleRedemption.cancelledPartial = true;
                return singleRedemption;
            }

            _contractsCache.sortedTroves.reInsert(
                _borrower,
                newICR,
                _upperPartialRedemptionHint,
                _lowerPartialRedemptionHint
            );

            troveManager.updateTroveDebt(_borrower, newDebt);
            for (uint256 i = 0; i < colls.tokens.length; i++) {
                colls.amounts[i] = finalAmounts[i];
            }
            troveManager.updateTroveCollTMR(_borrower, colls.tokens, colls.amounts);
            troveManager.updateStakeAndTotalStakes(_borrower);

            emit TroveUpdated(
                _borrower,
                newDebt,
                colls.tokens,
                finalAmounts,
                TroveManagerOperation.redeemCollateral
            );
        }

        return singleRedemption;
    }

    /*
     * @notice
     * Called when a full redemption occurs, and closes the trove.
     * The redeemer swaps (debt - liquidation reserve) YUSD for (debt - liquidation reserve) worth of Collateral, so the YUSD liquidation reserve left corresponds to the remaining debt.
     * In order to close the trove, the YUSD liquidation reserve is burned, and the corresponding debt is removed from the active pool.
     * The debt recorded on the trove's struct is zero'd elswhere, in _closeTrove.
     * Any surplus Collateral left in the trove, is sent to the Coll surplus pool, and can be later claimed by the borrower.
     */
    /// @param _contractsCache The contracts cache, used to get the contracts
    /// @param _borrower Address of trove owner being redeemed against
    /// @param _YUSD The amount of YUSD being redeemed/burned
    /// @param _remainingColls The remaining collateral in trove after being fully redeemed against. Available for the borrower to claim.
    /// @param _remainingCollsAmounts The amounts of the remaining collateral in trove after being fully redeemed against. Available for the borrower to claim.
    function _redeemCloseTrove(
        ContractsCache memory _contractsCache,
        address _borrower,
        uint256 _YUSD,
        address[] memory _remainingColls,
        uint256[] memory _remainingCollsAmounts
    ) internal {
        _contractsCache.yusdToken.burn(gasPoolAddress, _YUSD);
        // Update Active Pool YUSD, and send Collateral to account
        _contractsCache.activePool.decreaseYUSDDebt(_YUSD);

        // send Collaterals from Active Pool to CollSurplus Pool
        _contractsCache.collSurplusPool.accountSurplus(
            _borrower,
            _remainingColls,
            _remainingCollsAmounts
        );
        _contractsCache.activePool.sendCollaterals(
            address(_contractsCache.collSurplusPool),
            _remainingColls,
            _remainingCollsAmounts
        );
    }

    /*
     * @notice
     * This function has two impacts on the baseRate state variable:
     * 1) decays the baseRate based on time passed since last redemption or YUSD borrowing operation.
     * then,
     * 2) increases the baseRate based on the amount redeemed, as a proportion of total supply
     */
    /// @param _YUSDDrawn The amount of YUSD redeemed from the borrower's account
    /// @param _totalYUSDSupply The total supply of YUSD
    /// @return uint256 The new baseRate
    function _updateBaseRateFromRedemption(uint256 _YUSDDrawn, uint256 _totalYUSDSupply)
    internal
    returns (uint256)
    {
        uint256 decayedBaseRate = troveManager.calcDecayedBaseRate();

        /* Convert the drawn Collateral back to YUSD at face value rate (1 YUSD:1 USD), in order to get
         * the fraction of total supply that was redeemed at face value. */
        uint256 redeemedYUSDFraction = _YUSDDrawn.mul(10**18).div(_totalYUSDSupply);

        uint256 newBaseRate = decayedBaseRate.add(redeemedYUSDFraction.div(BETA));
        newBaseRate = LiquityMath._min(newBaseRate, DECIMAL_PRECISION); // cap baseRate at a maximum of 100%

        troveManager.updateBaseRate(newBaseRate);
        return newBaseRate;
    }
    /// @notice checks if the address being redeemed against is the lowest ICR in the sortedTroves list.
    /// @param _sortedTroves The address of the sortedTroves contract. This is where the doubly linked list of troves are stored.
    /// @param _firstRedemptionHint The address of the trove being redeemed. This should be the first element in the sortedTroves list.
    /// @return bool True if the address being redeemed against is the lowest ICR in the sortedTroves list.
    function _isValidFirstRedemptionHint(ISortedTroves _sortedTroves, address _firstRedemptionHint)
    internal
    view
    returns (bool)
    {
        if (
            _firstRedemptionHint == address(0) ||
            !_sortedTroves.contains(_firstRedemptionHint) ||
            troveManager.getCurrentICR(_firstRedemptionHint) < MCR
        ) {
            return false;
        }

        address nextTrove = _sortedTroves.getNext(_firstRedemptionHint);
        return nextTrove == address(0) || troveManager.getCurrentICR(nextTrove) < MCR;
    }
    /// @notice checks if the fee is lower than or equal to max fee specified by user
    /// @param _actualFee The fee being paid
    /// @param _maxFee The max fee specified by user
    function _requireUserAcceptsFeeRedemption(uint256 _actualFee, uint256 _maxFee) internal pure {
        require(_actualFee <= _maxFee, "User must accept fee");
    }
    /// @notice checks if the fee rate is >0.5% and <100%
    /// @param _YUSDAmount the amount of YUSD being redeemed
    /// @param _maxYUSDFee the max amount of YUSD being paid as a fee
    function _requireValidMaxFee(uint256 _YUSDAmount, uint256 _maxYUSDFee) internal pure {
        uint256 _maxFeePercentage = _maxYUSDFee.mul(DECIMAL_PRECISION).div(_YUSDAmount);
        require(_maxFeePercentage >= REDEMPTION_FEE_FLOOR, "Max fee must be at least 0.5%");
        require(_maxFeePercentage <= DECIMAL_PRECISION, "Max fee must be at most 100%");
    }
    /// @notice prevents redemptions during bootstrapping
    function _requireAfterBootstrapPeriod() internal view {
        uint256 systemDeploymentTime = yetiTokenContract.getDeploymentStartTime();
        require(
            block.timestamp >= systemDeploymentTime.add(BOOTSTRAP_PERIOD),
            "TroveManager: Redemptions are not allowed during bootstrap phase"
        );
    }
    /// @notice prevents redemptions if the protocol is insolvent
    function _requireTCRoverMCR() internal view {
        require(_getTCR() >= MCR, "TroveManager: Cannot redeem when TCR < MCR");
    }
    /// @notice checks if amount is >0
    /// @param _amount amount being checked
    function _requireAmountGreaterThanZero(uint256 _amount) internal pure {
        require(_amount > 0, "TroveManager: Amount must be greater than zero");
    }
    /// @notice checks if the redeemer has enough to cover the redemption fee + amount being redeemed
    /// @param _yusdToken address of YUSD token
    /// @param _redeemer address of redeemer
    /// @param _amount amount being redeemed + fee
    function _requireYUSDBalanceCoversRedemption(
        IYUSDToken _yusdToken,
        address _redeemer,
        uint256 _amount
    ) internal view {
        require(
            _yusdToken.balanceOf(_redeemer) >= _amount,
            "TroveManager: Requested redemption amount must be <= user's YUSD token balance"
        );
    }
    /// @notice makes sure collaterals being provided is nonzero
    /// @param coll the collaterals given during redemption
    function _requireNonZeroRedemptionAmount(newColls memory coll) internal pure {
        uint256 total = 0;
        for (uint256 i = 0; i < coll.amounts.length; i++) {
            total = total.add(coll.amounts[i]);
        }
        require(total > 0, "must be non zero redemption amount");
    }
    /// @notice only TroveManager can call this function
    function _requireCallerisTroveManager() internal view {
        require(msg.sender == troveManagerAddress);
    }
    /// @notice calculated YUSD fee based on the amount being redeemed
    /// @param _YUSDRedeemed The amount of YUSD redeemed from the borrower's account
    /// @return uint256 The YUSD fee
    function _getRedemptionFee(uint256 _YUSDRedeemed) internal view returns (uint256) {
        return _calcRedemptionFee(troveManager.getRedemptionRate(), _YUSDRedeemed);
    }
    /// @notice calculates the YUSD fee based on the current redemption rate and the amount being redeemed
    /// @param _redemptionRate The current redemption rate
    /// @param _YUSDRedeemed The amount of YUSD redeemed from the borrower's account
    /// @return uint256 The YUSD fee
    function _calcRedemptionFee(uint256 _redemptionRate, uint256 _YUSDRedeemed)
    internal
    pure
    returns (uint256)
    {
        uint256 redemptionFee = _redemptionRate.mul(_YUSDRedeemed).div(DECIMAL_PRECISION);
        require(
            redemptionFee < _YUSDRedeemed,
            "TroveManager: Fee would eat up all returned collateral"
        );
        return redemptionFee;
    }
    /// @notice calculated redemption rate based on baseRate
    /// @param _baseRate The current base rate
    /// @return uint256 The redemption rate
    function _calcRedemptionRate(uint256 _baseRate) internal pure returns (uint256) {
        return
        LiquityMath._min(
            REDEMPTION_FEE_FLOOR.add(_baseRate),
            DECIMAL_PRECISION // cap at a maximum of 100%
        );
    }
}
